BasicGame
    SpriteSet

        platforms > solid=True
            elevator > Missile orientation=UP speed=0.1 img=newset/floor3
            cloud    > Passive img=newset/cloud2 physicstype=CONT gravity=1.0 friction=0.02
            ground > Immovable color=DARKGRAY

        moving > physicstype=CONT gravity=1.0
            avatar > PlatformerAvatar img=newset/man1 mass=1 friction=0.1 jump_strength=16
            evil   >  orientation=LEFT gravity=0.5
                goomba     > Walker img=newset/zombie friction=0.0
                paratroopa > WalkerJumper img=newset/shark2 mass=1 friction=0.1 jump_strength=5

        goal > Immovable img=oryx/mushroom2
        fire > Immovable img=oryx/fire1
        coin > Immovable img=oryx/gold1

        wall > Immovable solid=False color=DARKGRAY
            
    TerminationSet
        SpriteCounter stype=goal  win=True
        SpriteCounter stype=avatar    win=False
           
    InteractionSet

        coin avatar > killSprite scoreChange=1
        evil avatar > killIfFromAbove scoreChange=1
        avatar evil > killIfAlive
        evil EOS  > killSprite

        goal avatar > killSprite

        moving elevator > pullWithIt
        evil wall ground > wallReverse

        elevator EOS > wrapAround

        cloud avatar > bounceForward
        cloud wall ground fire > wallStop
        avatar wall ground > wallStop

        avatar cloud > wallStop
        avatar EOS fire > killSprite
        
    LevelMapping
        G > goal
        1 > goomba
        2 > paratroopa
        = > elevator
        l > cloud
        f > fire
        c > coin
        . > ground