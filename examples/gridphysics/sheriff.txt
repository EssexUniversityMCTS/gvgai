BasicGame
    SpriteSet

        base    > Immovable    color=WHITE img=base
        avatar  > ShootAvatar stype=bullet
        surround > Immovable    color=YELLOW img=dirt invisible=True
        bullet > Missile img=explosion shrinkfactor=0.5 singleton=True

        turn > Immovable invisible=True hidden=True
            turnup > orientation=UP img=dirt
            turndown > orientation=DOWN img=dirt
            turnleft > orientation=LEFT img=dirt
            turnright > orientation=RIGHT img=dirt

        bandit > Bomber prob=0.1 cooldown=6 speed=1 img=alien
            bup > orientation=UP spawnorientation=LEFT stype=missile
            bdown > orientation=DOWN spawnorientation=RIGHT stype=missile
            bleft > orientation=LEFT spawnorientation=DOWN stype=missile
            bright > orientation=RIGHT spawnorientation=UP stype=missile

        missile > Missile img=bomb shrinkfactor=0.6 speed=0.6


    LevelMapping
        0 > base
        u > surround bup
        d > surround bdown
        l > surround bleft
        r > surround bright
        s > surround
        A > avatar
        1 > turnup
        2 > turnleft
        3 > turndown
        4 > turnright

    TerminationSet
        SpriteCounter      stype=avatar             limit=0 win=False
        SpriteCounter      stype=bandit             limit=0 win=True
        Timeout limit=1000 win=True
        
    InteractionSet
        avatar surround base wall > stepBack

        base missile bullet > killBoth
        EOS missile bullet > killSprite

        bleft turndown > transformTo stype=bdown
        bright turnup > transformTo stype=bup
        bdown turnright > transformTo stype=bright
        bup turnleft > transformTo stype=bleft

        bandit bullet > killBoth scoreChange=1
        avatar missile > killBoth scoreChange=-1

        bullet wall > killSprite