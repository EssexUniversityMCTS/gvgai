BasicGame
    SpriteSet
        forestDense > SpawnPoint stype=log prob=0.4  cooldown=10 img=forest
        forestSparse > SpawnPoint stype=log prob=0.1  cooldown=5 img=forest
        structure > Immovable
            water > color=BLUE img=water
            goal  > Door color=GREEN img=goal
        log    > Missile   orientation=LEFT  speed=0.1 color=BROWN img=log
        truck  > img=truck
            rightTruck  > Missile   orientation=RIGHT 
                fastRtruck  > speed=0.2  color=ORANGE
                slowRtruck  > speed=0.1  color=RED
            leftTruck  > Missile   orientation=LEFT
                fastLtruck  > speed=0.2  color=ORANGE
                slowLtruck  > speed=0.1  color=RED

        wall > Immovable color=BLACK img=wall
        
    InteractionSet
        goal avatar  > killSprite scoreChange=1
        avatar log   > shieldFrom ftype=killSprite stype=water
        avatar log   > pullWithIt
        avatar wall  > stepBack
        avatar truck water > killSprite
        log    EOS   > killSprite
        truck  EOS   > wrapAround
    
    TerminationSet
        SpriteCounter stype=goal   limit=0 win=True
        SpriteCounter stype=avatar limit=0 win=False
    
    LevelMapping
        g > goal
        0 > water
        1 > forestDense water       # note how a single character can spawn multiple sprites
        2 > forestDense wall log
        3 > forestSparse water       # note how a single character can spawn multiple sprites
        4 > forestSparse wall log
        - > slowRtruck
        x > fastRtruck
        _ > slowLtruck
        l > fastLtruck
        = > log water
        B > avatar log
