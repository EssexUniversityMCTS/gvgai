BasicGame key_handler=Pulse
    SpriteSet
        goal > Immovable img=door portal=true
        avatar > ShootAvatar stype=boulder rotateInPlace=true
        box    > Passive img=box
        mirror > Passive img=crystal
           mup > orientation=UP
           mdown > orientation=DOWN
           mright > orientation=RIGHT
           mleft > orientation=LEFT
           mswap >

        lasercannon > SpawnPoint 
            lcup > img=spaceship stype=lup
            lcdown > img=spaceship stype=ldown
            lcright > img=spaceship stype=lright
            lcleft > img=spaceship stype=lleft
            lcswap > img=spaceship stype=lleft
        laser > Missile color=YELLOW  shrinkfactor=0.5 speed=0.5
            lup > orientation=UP
            ldown > orientation=DOWN
            lright > orientation=RIGHT
            lleft > orientation=LEFT
        boulder > Missile color=BLUE singleton=True img=bullet
        obstacle > Immovable img=damaged

    LevelMapping
        A > avatar
        b > box
        m > lcleft
        s > lcright
        t > lcup
        e > lcdown
        g > goal
        u > mup
        d > mdown
        r > mright
        l > mleft
        o > obstacle

    InteractionSet

        avatar wall obstacle lasercannon > stepBack

        avatar box mirror > shieldFrom ftype=killIfFrontal stype=laser
        box avatar  > bounceForward
        mirror avatar  > bounceForward

        box wall box mirror obstacle lasercannon > undoAll
        mirror wall mirror box lasercannon > undoAll

        avatar wall > stepBack

        laser wall box boulder > killSprite
        obstacle laser > killSprite
        boulder wall lasercannon mirror EOS > killSprite

        avatar laser > killIfFrontal
        avatar laser > killIfNotFrontal


        goal avatar > killSprite scoreChange=5

        lcdown boulder > transformTo stype=lcswap
        lcleft boulder > transformTo stype=lcdown
        lcup boulder > transformTo stype=lcleft
        lcright boulder > transformTo stype=lcup
        lcswap boulder > transformTo stype=lcright

        laser mirror > attractGaze align=True
        mdown boulder > transformTo stype=mswap
        mleft boulder > transformTo stype=mdown
        mup boulder > transformTo stype=mleft
        mright boulder > transformTo stype=mup
        mswap boulder > transformTo stype=mright


    TerminationSet
        SpriteCounter stype=avatar    limit=0 win=False
        SpriteCounter stype=goal    limit=0 win=True