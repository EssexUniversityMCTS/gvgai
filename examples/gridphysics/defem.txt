BasicGame
    SpriteSet
        cannon > Passive invisible=TRUE hidden=TRUE
        avatar > MovingAvatar img=avatar
        bullet > RandomMissile img=bullet

        enemyW >
            enemy   >
                randomEnemy > RandomNPC cooldown=5 img=bee
                chaseEnemy  > Chaser cooldown=5 stype=avatar img=monster
                dirEnemy   > Missile cooldown=5 img=camel

            spawner >
                spawnRandom > SpawnPoint stype=randomEnemy total=10 cooldown=20 img=portal
                spawnChase  > SpawnPoint stype=chaseEnemy total=10 cooldown=40 img=hole

            dirSpawner > SpawnPoint stype=dirEnemy total=5 cooldown=30 img=door
                spawnHorzR  > orientation=RIGHT
                spawnHorzL  > orientation=LEFT
                spawnHorzU  > orientation=UP
                spawnHorzD  > orientation=DOWN

    TerminationSet
        SpriteCounter stype=avatar limit=0 win=False
        SpriteCounter stype=enemy limit=0 win=True

    InteractionSet

        cannon avatar      > spawnIfHasMore stype=bullet resource=null limit=0 prob=0.5
        enemy bullet       > killSprite scoreChange=1
        avatar enemy       > killSprite
        EOS avatar cannon  > stepBack
        dirEnemy EOS       > reverseDirection
        bullet enemy EOS   > killSprite


    LevelMapping
        a > avatar cannon
        r > spawnRandom cannon
        c > spawnChase cannon
        z > spawnHorzR cannon
        x > spawnHorzL cannon
        f > spawnHorzD cannon
        v > spawnHorzU cannon
        . > cannon