BasicGame
	SpriteSet
		sword > Flicker color=LIGHTGRAY limit=1 singleton=True img=sword.png
		dirt > Immovable color=BROWN img=dirt.png
		exitdoor > Door color=GREEN img=door.png
		diamond > Resource color=YELLOW limit=10 shrinkfactor=0.75 img=diamond.png
		boulder > Missile orientation=DOWN color=GRAY speed=0.2 img=boulder.png
		moving >
			avatar  > ShootAvatar   stype=sword img=avatar.png
			enemy > RandomNPC
				crab > color=RED img=camel.png
				butterfly > color=PINK img=butterfly.png
	LevelMapping
		. > dirt
		E > exitdoor
		o > boulder
		x > diamond
		c > crab
		b > butterfly
	InteractionSet
		dirt avatar > killSprite
		dirt sword  > killSprite
		diamond avatar > collectResource
		diamond avatar > killSprite scoreChange=2
		moving wall > stepBack
		moving boulder > stepBack
		avatar boulder > killIfFromAbove scoreChange=-1
		avatar butterfly > killSprite scoreChange=-1
		avatar crab > killSprite scoreChange=-1
		boulder dirt > stepBack
		boulder wall > stepBack
		boulder diamond > stepBack
		boulder boulder > stepBack
		enemy dirt > stepBack
		enemy diamond > stepBack
		crab butterfly > killSprite
		butterfly crab > transformTo stype=diamond scoreChange=1
		exitdoor avatar > killIfOtherHasMore resource=diamond limit=9

	TerminationSet
		SpriteCounter stype=avatar limit=0 win=False
		SpriteCounter stype=exitdoor limit=0 win=True